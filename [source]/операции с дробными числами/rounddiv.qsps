QSP-Game Округляет число до указанной разрядности
$args[0] - число
$args[1] - разрядность возможны варианты 1, 10, 100, 1000 и т.п., и 0.1, 0.01 и т.п.
$args[2] - [up] в сторону увеличения [down] - с округлением-отсечением
!@pp:if(em_rounddiv):include
# #rounddiv#
$args[9] = $args[9]	& !@ инициализация аргументов
if args[1]<>0: $args[1]=$str(args[1])	& !@ если разрядность указана числом, переводим в строковый вид
if $args[1]='' or $args[1]='0': $args[1]='1'
$result=$args[0]
if instr($args[0],'.')=0: exit	& !@ почему-то если число целое, то не округляется. Это странно. Ведь оно может округляться до сотен
if instr($args[1],'.')<>0:
	!@ если округляем дробную часть
	if (func('#razdiv#',$args[0])<=func('#razdiv#',$args[1])):
		!@ если разрядов в дробной части числа меньше,
		!@ чем разрядов в дробной части числа разрядности
		exit	& !@ округлять нет смысла
	end
	!@ округляем число, переведя его в целое, что соответственно накладывает ограничение на рациональное число
	args['res'] = func('#even#',val($replace($args[0],'.')),func('#razdiv#',$args[0])/func('#razdiv#',$args[1]),$args[2])
	$result = $func('#indiv#',args['res'],func('#razdiv#',$args[0]))
	exit
else
	args['res'] = func('#even#',val($replace($args[0],'.')),func('#razdiv#',$args[0])*func('#raz#',val($args[1])),$args[2])
	$result = $func('#indiv#',args['res'],func('#razdiv#',$args[0]))
	exit
end
--- #rounddiv# ---------------------------------
!@pp:endif